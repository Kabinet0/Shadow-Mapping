// Thanks: https://www.reddit.com/r/vulkan/comments/y6sz28/point_light_shadow_mapping_using_cube_maps/

// Suppress annoying warning
#pragma warning (disable : 3078)

#pragma use_dxc
#pragma enable_d3d11_debug_symbols

// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CubemapBlurX

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
Texture2D<float2> _InputBuffer;
RWTexture2D<float2> _OutputBuffer;

StructuredBuffer<float2> _CubeSliceOffsets;
StructuredBuffer<int2> _CubemapData; // X is resolution, Y is _FovPaddingPx

int _Radius;

#define POS_X 0
#define NEG_X 1
#define POS_Y 2
#define NEG_Y 3
#define POS_Z 4
#define NEG_Z 5

uint2 LeadingFace(uint x, uint y, uint face, uint faceIndex, uint _Resolution)
{
    switch (face) {
    case POS_X:
        return uint2(x, y) + _CubeSliceOffsets[NEG_Z + faceIndex];
    case NEG_X:
        // Negative X
        return uint2(x, y) + _CubeSliceOffsets[POS_Z + faceIndex];
    case POS_Y:
        // Positive Y
        return uint2(y, _Resolution - (x + 1)) + _CubeSliceOffsets[POS_X + faceIndex]; // Right for leading
    case NEG_Y:
        // Negative Y
        return uint2(_Resolution - (y + 1), x) + _CubeSliceOffsets[POS_X + faceIndex]; // Right for leading
    case POS_Z:
        // Positive Z
        return uint2(x, y) + _CubeSliceOffsets[POS_X + faceIndex];
    case NEG_Z:
        // Negative Z
        return uint2(x, y) + _CubeSliceOffsets[NEG_X + faceIndex];


    default:
        // Negative Z
        return uint2(x, y) + _CubeSliceOffsets[NEG_X + faceIndex];
    }
}

int2 TrailingFace(uint x, uint y, uint face, uint faceIndex, uint _Resolution)
{
    switch (face) {
    case POS_X:
        return uint2(x, y) + _CubeSliceOffsets[POS_Z + faceIndex];
    case NEG_X:
        // Negative X
        return uint2(x, y) + _CubeSliceOffsets[NEG_Z + faceIndex];
    case POS_Y:
        // Positive Y
        return uint2(_Resolution - (y + 1), x) + _CubeSliceOffsets[NEG_X + faceIndex]; // Right for trailing
    case NEG_Y:
        // Negative Y
        return uint2(y, _Resolution - (x + 1)) + _CubeSliceOffsets[NEG_X + faceIndex]; // Right for trailing
    case POS_Z:
        // Positive Z
        return uint2(x, y) + _CubeSliceOffsets[NEG_X + faceIndex];
    case NEG_Z:
        // Negative Z
        return uint2(x, y) + _CubeSliceOffsets[POS_X + faceIndex];



    default:
        // Negative Z
        return uint2(x, y) + _CubeSliceOffsets[POS_X + faceIndex];
    }
}

[numthreads(1,64,1)]
void CubemapBlurX (uint3 id : SV_DispatchThreadID)
{
    int _Resolution;
    int _FovPaddingPx;
    uint _CubeIndex;

    // Structured Buffer data
    uint cubeCount, stride;
    _CubemapData.GetDimensions(cubeCount, stride);

    // Determine data for this rowID
    uint RowIDSum = 0;
    for (uint cubeID = 0; cubeID < cubeCount; cubeID++) {
        RowIDSum += _CubemapData[cubeID].x;
        if (id.y < RowIDSum) {
            // Do stuff
            _Resolution = _CubemapData[cubeID].x;
            _FovPaddingPx = _CubemapData[cubeID].y;
            _CubeIndex = cubeID;

            break;
        }
    }


    uint face = id.z;
    uint faceIndex = 6 * _CubeIndex;
    uint y = id.y - (RowIDSum - _Resolution);

    float2 FaceOffset = _CubeSliceOffsets[face + faceIndex];
    float2 Accumulator = float2(0, 0);

    float per_Texel = 1 / float(2 * _Radius + 1);


    // Accumulate last part of trailing face
    for (int x = _Resolution - _Radius; x < _Resolution; x++) {
        Accumulator += _InputBuffer[TrailingFace(x - (_FovPaddingPx * 2), y, face, faceIndex, _Resolution)];
    }

    // Accumulate first part of current face
    for (int x = 0; x < _Radius; x++) {
        Accumulator += _InputBuffer[uint2(x, y) + FaceOffset];
    }

    // Blur first part of face, accounting for trailing face
    for (int x = 0; x < _Radius; x++) {
        Accumulator += _InputBuffer[uint2(x + _Radius, y) + FaceOffset];
        _OutputBuffer[uint2(x, y) + FaceOffset] = Accumulator * per_Texel;
        //_OutputBuffer[uint2(x, y) + FaceOffset] = float2(0, 1);
        //_OutputBuffer[uint2(x, y) + FaceOffset] = TrailingFace((_Resolution - _Radius) + x, id);
        Accumulator -= _InputBuffer[TrailingFace((_Resolution - _Radius - (_FovPaddingPx * 2)) + x, y, face, faceIndex, _Resolution)];
    }

    // Blur main part of face
    for (int x = _Radius; x < _Resolution - _Radius; x++) {
        Accumulator += _InputBuffer[uint2(x + _Radius, y) + FaceOffset];
        _OutputBuffer[uint2(x, y) + FaceOffset] = Accumulator * per_Texel;
        //_OutputBuffer[uint2(x, y) + FaceOffset] = float2(0,0);
        Accumulator -= _InputBuffer[uint2(x - _Radius, y) + FaceOffset];
    }

    // Blur over into leading face
    for (int x = _Resolution - _Radius; x < _Resolution; x++) {
        Accumulator += _InputBuffer[LeadingFace((x + _Radius) - _Resolution + (_FovPaddingPx * 2), y, face, faceIndex, _Resolution)];
        _OutputBuffer[uint2(x, y) + FaceOffset] = Accumulator * per_Texel;
        Accumulator -= _InputBuffer[uint2(x - _Radius, y) + FaceOffset];
    }
}
